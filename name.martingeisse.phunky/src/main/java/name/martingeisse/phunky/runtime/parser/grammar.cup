
// ------------------------------------------------------------------------------------
// --- header information
// ------------------------------------------------------------------------------------

import java_cup.runtime.*;
import name.martingeisse.phunky.runtime.code.*;
import name.martingeisse.phunky.runtime.code.expression.*;
import name.martingeisse.phunky.runtime.code.expression.array.*;
import name.martingeisse.phunky.runtime.code.expression.oop.*;
import name.martingeisse.phunky.runtime.code.expression.operator.*;
import name.martingeisse.phunky.runtime.code.statement.*;
import name.martingeisse.phunky.runtime.code.declaration.*;
import java_cup.runtime.ComplexSymbolFactory.ComplexSymbol;
import java_cup.runtime.Symbol;
import java.util.List;
import java.util.ArrayList;
import org.apache.commons.lang3.tuple.Pair;
import org.apache.commons.lang3.NotImplementedException;

// scanner behavior
init with {:
:};
scan with {:
	return getScanner().next_token();
:};
parser code {:

	/* (non-Javadoc)
	 * @see java_cup.runtime.lr_parser#syntax_error(java_cup.runtime.Symbol)
	 */
	@Override
	public void syntax_error(final Symbol token) {
		if (token instanceof ComplexSymbol) {
			ComplexSymbol complexSymbol = (ComplexSymbol)token;
			syntax_error(complexSymbol.getLeft(), "unexpected symbol: " + complexSymbol.sym + ", value " + complexSymbol.value);
			System.err.print("Valid symbols at this point:");
			int state = ((Symbol)stack.peek()).parse_state;
			short[] actionRow = action_tab[state];
			for (int i=0; i<actionRow.length; i+=2) {
				if (actionRow[i + 1] != 0) {
					System.err.print(" " + actionRow[i]);
				}
			}
			System.err.println();
		} else {
			super.syntax_error(token);
		}
	}

	/**
	 * Helper method for custom syntax errors. This method will NOT show possible
	 * transitions from the action table since it assumes that the 'state' variable
	 * does not have a predictable value.
	 */
	public void syntax_error(final Location location, final String message) {
		int line = location.getLine();
		int column = location.getColumn();
		System.err.println("Syntax error at line " + (line + 1) + ", col " + (column + 1) + ": " + message);
	}

	// extracts proper switch cases from syntactical case constructs
	SwitchCase[] extractProperCases(List<SwitchCase> switchCases) {
		int properCaseCount = 0;
		for (SwitchCase switchCase : switchCases) {
			if (switchCase.getExpression() != null) {
				properCaseCount++;
			}
		}
		SwitchCase[] result = new SwitchCase[properCaseCount];
		int i=0;
		for (SwitchCase switchCase : switchCases) {
			if (switchCase.getExpression() != null) {
				result[i] = switchCase;
				i++;
			}
		}
		return result;
	}
	
	// extracts the statements for the effective default switch case from syntactical case constructs
	Statement extractDefaultCaseStatement(List<SwitchCase> switchCases) {
		Statement result = null;
		for (SwitchCase switchCase : switchCases) {
			if (switchCase.getExpression() == null) {
				result = switchCase.getStatement();
				// don't break -- for multiple default cases, PHP uses the last one
			}
		}
		return result;
	}
	
	// obtains the current location. This isn't very nice as it obtains the location of the *last*
	// symbol of a production, not the first one, but this is easier to implement
	CodeLocation obtainLocation() {
		ComplexSymbolFactory.ComplexSymbol symbol = (ComplexSymbolFactory.ComplexSymbol)stack.peek();
		Location location = symbol.getLeft();
		if (location == null) {
			return null;
		} else {
			return new CodeLocation(location.getUnit(), location.getLine(), location.getColumn());
		}
	}
	
:}

action code {:

	// location setting for statements
	<T extends Statement> T setLocation(T statement) {
		CodeLocation location = parser.obtainLocation();
		if (location != null) {
			statement.setLocation(location);
		}
		return statement;
	}
	
	// location setting for expressions
	<T extends Expression> T setLocation(T expression) {
		CodeLocation location = parser.obtainLocation();
		if (location != null) {
			expression.setLocation(location);
		}
		return expression;
	}

:}

// ------------------------------------------------------------------------------------
// --- symbols
// ------------------------------------------------------------------------------------

// embedding in verbatim content
terminal String VERBATIM_CONTENT;

// punctuation (some of these can be operators too)
terminal OPENING_PARENTHESIS, CLOSING_PARENTHESIS;
terminal OPENING_CURLY_BRACE, CLOSING_CURLY_BRACE;
terminal OPENING_SQUARE_BRACKET, CLOSING_SQUARE_BRACKET;
terminal RIGHT_ARROW, DOUBLE_RIGHT_ARROW;
terminal SEMICOLON;
terminal SCOPE_RESOLUTION;
terminal QUESTION_MARK;
terminal COLON;
terminal COMMA;

// compilation object keywords
terminal FUNCTION, CLASS, INTERFACE, EXTENDS, IMPLEMENTS;

// modifier keywords
terminal PUBLIC, PROTECTED, PRIVATE;
terminal STATIC, ABSTRACT, FINAL;
terminal VAR, CONST;

// statement keywords
terminal FOR, FOREACH, AS, DO, WHILE, BREAK;
terminal IF, ELSEIF, ELSE;
terminal RETURN;
terminal THROW, TRY, CATCH, FINALLY;
terminal SWITCH, CASE, DEFAULT;
terminal GLOBAL;
terminal ECHO;

// computation and logical operators
terminal PLUS, MINUS, TIMES, DIVIDE, MOD, CONCAT, POWER;
terminal LOGICAL_NOT, LOGICAL_SHORTCUT_AND, LOGICAL_SHORTCUT_OR;
terminal BITWISE_NOT, BITWISE_AND, BITWISE_OR, BITWISE_XOR;
terminal LOW_PRECEDENCE_LOGICAL_SHORTCUT_AND, LOW_PRECEDENCE_LOGICAL_SHORTCUT_OR, LOW_PRECEDENCE_LOGICAL_XOR;
terminal SHIFT_LEFT, SHIFT_RIGHT;

// comparison operators
terminal EQUAL, NOT_EQUAL, IDENTICAL, NOT_IDENTICAL;
terminal GREATER, LESS, GREATER_EQUAL, LESS_EQUAL;

// assignment operators
terminal ASSIGN;
terminal INCREMENT, DECREMENT;
terminal PLUS_ASSIGN, MINUS_ASSIGN, TIMES_ASSIGN, DIVIDE_ASSIGN, MOD_ASSIGN, CONCAT_ASSIGN;
terminal BITWISE_AND_ASSIGN, BITWISE_OR_ASSIGN, BITWISE_XOR_ASSIGN;
terminal SHIFT_LEFT_ASSIGN, SHIFT_RIGHT_ASSIGN;

// special expressions and operators
terminal ARRAY;
terminal SELF;
terminal PARENT;
terminal NEW;
terminal INSTANCEOF;
terminal UNSET;
terminal String CAST_TYPE_NAME;
terminal SUPPRESS_ERRORS;
terminal terminal INCLUDE, INCLUDE_ONCE, REQUIRE, REQUIRE_ONCE, PRINT;

// primitive expressions
terminal NULL_LITERAL;
terminal Boolean BOOLEAN_LITERAL;
terminal Number INTEGER_LITERAL;
terminal Double FLOAT_LITERAL;
terminal String STRING_LITERAL;
terminal String IDENTIFIER;
terminal String LOCAL_VARIABLE_SINGLE_INDIRECTION;
terminal LocalVariableExpression LOCAL_VARIABLE_MULTI_INDIRECTION;

// non terminals (general)
non terminal StatementSequence program;
non terminal Statement block;
non terminal List<Statement> statements;
non terminal Statement statement;
non terminal Statement simple_statement_without_semicolon;
non terminal Statement if_statement_continuation;
non terminal List<SwitchCase> switch_cases;
non terminal SwitchCase switch_case;
non terminal Void catch_clauses;
non terminal Void catch_clause;
non terminal Void optional_finally_clause;
non terminal List<Object> static_statement_variables;
non terminal Object static_statement_variable;
non terminal List<Expression> expressions;
non terminal List<Expression> one_or_more_expressions;
non terminal Expression expression;
non terminal List<Pair<Expression, Expression>> array_literal_elements;
non terminal List<Pair<Expression, Expression>> one_or_more_array_literal_elements;
non terminal Pair<Expression, Expression> array_literal_element;
non terminal List<ParameterDeclaration> parameter_declarations;
non terminal List<ParameterDeclaration> one_or_more_parameter_declarations;
non terminal ParameterDeclaration parameter_declaration;
non terminal Expression potentiallyDynamicName;

// non terminals (OOP)
non terminal OopTypeDefinition oop_type_definition;
non terminal String oop_class_extends_clause;
non terminal List<String> oop_class_implements_clause;
non terminal List<String> oop_interface_extends_clause;
non terminal List<String> interface_references;
non terminal List<String> one_or_more_interface_references;
non terminal String interface_reference;
non terminal List<OopMemberDefinition> oop_member_definitions;
non terminal OopMemberDefinition oop_member_definition;
non terminal List<OopEntityModifier> oop_entity_modifiers;
non terminal OopEntityModifier oop_entity_modifier;

// precedences
precedence left SEMICOLON; // needed for error recovery
precedence right ELSE, ELSEIF; // to resolve nested if/else without curly braces
precedence left LOW_PRECEDENCE_LOGICAL_SHORTCUT_OR;
precedence left LOW_PRECEDENCE_LOGICAL_XOR;
precedence left LOW_PRECEDENCE_LOGICAL_SHORTCUT_AND;
precedence right ASSIGN, PLUS_ASSIGN, MINUS_ASSIGN, TIMES_ASSIGN, DIVIDE_ASSIGN, MOD_ASSIGN, CONCAT_ASSIGN, BITWISE_AND_ASSIGN, BITWISE_OR_ASSIGN, BITWISE_XOR_ASSIGN, SHIFT_LEFT_ASSIGN, SHIFT_RIGHT_ASSIGN;
precedence left QUESTION_MARK, COLON; // yes, *left*. PHP sucks.
precedence left LOGICAL_SHORTCUT_OR;
precedence left LOGICAL_SHORTCUT_AND;
precedence left BITWISE_OR;
precedence left BITWISE_XOR;
precedence left BITWISE_AND;
precedence nonassoc EQUAL, NOT_EQUAL, IDENTICAL, NOT_IDENTICAL, GREATER, LESS, GREATER_EQUAL, LESS_EQUAL; // unlike PHP, use same precendence for all of them to avoid simple errors
precedence left SHIFT_LEFT, SHIFT_RIGHT;
precedence left PLUS, MINUS, CONCAT;
precedence left TIMES, DIVIDE, MOD;
precedence right LOGICAL_NOT;
precedence nonassoc INSTANCEOF;
precedence right INCREMENT, DECREMENT, BITWISE_NOT, OPENING_PARENTHESIS, SUPPRESS_ERRORS;
precedence left POWER;
precedence left OPENING_SQUARE_BRACKET, RIGHT_ARROW;
// TODO: clone

// ------------------------------------------------------------------------------------
// --- general productions
// ------------------------------------------------------------------------------------

start with program;

program ::=
	statements:sts
		{: RESULT = setLocation(new StatementSequence(sts)); :}
;

// ------------------------------------------------------------------------------------
// --- statement productions
// ------------------------------------------------------------------------------------

block ::=
	OPENING_CURLY_BRACE statements:sts CLOSING_CURLY_BRACE
		{: RESULT = setLocation(new StatementSequence(sts)); :}
;

statements ::=
		{: RESULT = new ArrayList<Statement>(); :}
	| statements:sts statement:st
		{: sts.add(st); RESULT = sts; :}
;

statement ::=

	// simple statements
	simple_statement_without_semicolon:s SEMICOLON
		{: RESULT = s; :} 
	| block:b
		{: RESULT = b; :}
	| VERBATIM_CONTENT:c
		{: RESULT = setLocation(new ExpressionStatement(new FunctionCall("echo", new LiteralExpression(c)))); :}
		
	// build-in special statements
	| ECHO one_or_more_expressions:exs SEMICOLON
		{: RESULT = setLocation(new ExpressionStatement(setLocation(new FunctionCall("echo", exs.toArray(new Expression[exs.size()]))))); :}
	| GLOBAL LOCAL_VARIABLE_SINGLE_INDIRECTION:v SEMICOLON
		{: RESULT = setLocation(new GlobalStatement(v)); :}
	| STATIC static_statement_variables:vars SEMICOLON
		{: RESULT = null; /* TODO */ :}

	// control structures
	| IF if_statement_continuation:c
		{: RESULT = c; :}
	| FOR OPENING_PARENTHESIS simple_statement_without_semicolon:init SEMICOLON expression:cond SEMICOLON simple_statement_without_semicolon:advance CLOSING_PARENTHESIS block:body
		{: RESULT = setLocation(new ForStatement(init, cond, advance, body)); :}
	| FOR OPENING_PARENTHESIS simple_statement_without_semicolon:init SEMICOLON SEMICOLON simple_statement_without_semicolon:advance CLOSING_PARENTHESIS block:body
		{: RESULT = setLocation(new ForStatement(init, setLocation(new LiteralExpression(true)), advance, body)); :}
	| FOREACH OPENING_PARENTHESIS expression:e AS LOCAL_VARIABLE_SINGLE_INDIRECTION:v CLOSING_PARENTHESIS block:body
		{: RESULT = setLocation(new ForeachStatement(e, null, v, body)); :}
	| FOREACH OPENING_PARENTHESIS expression:e AS LOCAL_VARIABLE_SINGLE_INDIRECTION:k DOUBLE_RIGHT_ARROW LOCAL_VARIABLE_SINGLE_INDIRECTION:v CLOSING_PARENTHESIS block:body
		{: RESULT = setLocation(new ForeachStatement(e, k, v, body)); :}
	| WHILE OPENING_PARENTHESIS expression:e CLOSING_PARENTHESIS statement:s
		{: RESULT = setLocation(new WhileStatement(e, s)); :}
	| DO statement:s WHILE OPENING_PARENTHESIS expression:e CLOSING_PARENTHESIS
		{: RESULT = setLocation(new DoWhileStatement(s, e)); :}
	| BREAK SEMICOLON
		{: RESULT = setLocation(new BreakStatement()); :}
	| RETURN SEMICOLON
		{: RESULT = setLocation(new ReturnStatement(null)); :}
	| RETURN expression:e SEMICOLON
		{: RESULT = setLocation(new ReturnStatement(e)); :}
	| THROW expression:e SEMICOLON
		{: RESULT = setLocation(new ThrowStatement(e)); :}
	| TRY block:tryBody catch_clauses:ccs optional_finally_clause:fc
		{: if (1 == 1) throw new NotImplementedException(""); :}
	| SWITCH OPENING_PARENTHESIS expression:e CLOSING_PARENTHESIS OPENING_CURLY_BRACE switch_cases:cs CLOSING_CURLY_BRACE
		{:
			SwitchCase[] properCases = parser.extractProperCases(cs);
			Statement defaultCaseStatement = parser.extractDefaultCaseStatement(cs);
			RESULT = setLocation(new SwitchStatement(e, properCases, defaultCaseStatement));
		:}
	
	// definitions
	| FUNCTION IDENTIFIER:id OPENING_PARENTHESIS parameter_declarations:decls CLOSING_PARENTHESIS block:body
		{:
			RESULT = setLocation(new FunctionDefinition(id, decls.toArray(new ParameterDeclaration[decls.size()]), body));
		:}
	| oop_type_definition:def
		{: RESULT = def; :}
		
	// syntax error recovery
	| error SEMICOLON
		{: System.err.println("syntax error"); RESULT = setLocation(new NopStatement()); :}
;

static_statement_variables ::=
	static_statement_variable:v
		{: RESULT = null; /* TODO */ :}
	| static_statement_variables:vs COMMA static_statement_variable:v
		{: RESULT = null; /* TODO */ :}
;

static_statement_variable ::=
	LOCAL_VARIABLE_SINGLE_INDIRECTION:v
		{: RESULT = null; /* TODO */ :}
	| LOCAL_VARIABLE_SINGLE_INDIRECTION:v ASSIGN expression:e
		{: RESULT = null; /* TODO */ :}
;

simple_statement_without_semicolon ::=
		{: RESULT = setLocation(new NopStatement()); :}
	| expression:e
		{: RESULT = setLocation(new ExpressionStatement(e)); :}
;

if_statement_continuation ::=
	OPENING_PARENTHESIS expression:e CLOSING_PARENTHESIS statement:s
		{: RESULT = setLocation(new IfStatement(e, s, setLocation(new NopStatement()))); :}
	| OPENING_PARENTHESIS expression:e CLOSING_PARENTHESIS statement:s1 ELSE statement:s2
		{: RESULT = setLocation(new IfStatement(e, s1, s2)); :}
	| OPENING_PARENTHESIS expression:e CLOSING_PARENTHESIS statement:s ELSEIF if_statement_continuation:c
		{: RESULT = setLocation(new IfStatement(e, s, c)); :}
;

catch_clauses ::=
		{: if (1 == 1) throw new NotImplementedException(""); :}
	| catch_clauses:ccs catch_clause:cc
		{: if (1 == 1) throw new NotImplementedException(""); :}
;

catch_clause ::=
	CATCH OPENING_PARENTHESIS IDENTIFIER:id LOCAL_VARIABLE_SINGLE_INDIRECTION:v CLOSING_PARENTHESIS block:body
		{: if (1 == 1) throw new NotImplementedException(""); :}
;

optional_finally_clause ::=
		{: RESULT = null; :}
	| FINALLY 
		{: if (1 == 1) throw new NotImplementedException(""); :}
;

switch_cases ::=
		{: RESULT = new ArrayList<SwitchCase>(); :}
	| switch_cases:cs switch_case:c
		{: cs.add(c); RESULT = cs; :}
;

switch_case ::= 
	CASE expression:e COLON statements:sts
		{: RESULT = new SwitchCase(e, setLocation(new StatementSequence(sts))); :}
	| DEFAULT COLON statements:sts
		{: RESULT = new SwitchCase(null, setLocation(new StatementSequence(sts))); :}
;

// ------------------------------------------------------------------------------------
// --- expression productions
// ------------------------------------------------------------------------------------

expressions ::=
		{: RESULT = new ArrayList<Expression>(); :}
	| one_or_more_expressions:exs
		{: RESULT = exs; :}
;

one_or_more_expressions ::=
	expression:ex
		{: RESULT = new ArrayList<Expression>(); RESULT.add(ex); :}
	| one_or_more_expressions:exs COMMA expression:ex
		{: exs.add(ex); RESULT = exs; :}
;

expression ::=

	// parenthesized expressions
	OPENING_PARENTHESIS expression:e CLOSING_PARENTHESIS
		{: RESULT = e; :}

	// literals
	| NULL_LITERAL
		{: RESULT = setLocation(new LiteralExpression(null)); :}
	| BOOLEAN_LITERAL:e
		{: RESULT = setLocation(new LiteralExpression(e)); :}
	| INTEGER_LITERAL:e
		{: RESULT = setLocation(new LiteralExpression(e)); :}
	| STRING_LITERAL:e 
		{: RESULT = setLocation(new LiteralExpression(e)); :}
	| FLOAT_LITERAL:e 
		{: RESULT = setLocation(new LiteralExpression(e)); :}
	
	// constants and variables
	| IDENTIFIER:id
		{: RESULT = setLocation(new ConstantExpression(id)); :}
	| LOCAL_VARIABLE_SINGLE_INDIRECTION:id
		{: RESULT = setLocation(new LocalVariableExpression(id, 1)); :}
	| LOCAL_VARIABLE_MULTI_INDIRECTION:var
		{: RESULT = var; :}
	
	// type casting
	| OPENING_PARENTHESIS CAST_TYPE_NAME:type CLOSING_PARENTHESIS expression:e
		{:
			CastOperator op = ParserHelper.recognizeCastOperator(type);
			if (op == null) {
				parser.syntax_error(typexleft, "unknown type: " + type);				
				RESULT = e;
			} else {
				RESULT = setLocation(new CastExpression(op, e));
			}
		:} 
	| OPENING_PARENTHESIS ARRAY CLOSING_PARENTHESIS expression:e
		{: RESULT = setLocation(new CastExpression(CastOperator.ARRAY, e)); :}
	| OPENING_PARENTHESIS UNSET CLOSING_PARENTHESIS expression:e
		{: RESULT = setLocation(new CastExpression(CastOperator.NULL, e)); :}
	
	// unary operators
	| MINUS expression:e
		{: RESULT = setLocation(new UnaryExpression(UnaryOperator.NEGATE, e)); :} 
	| LOGICAL_NOT expression:e
		{: RESULT = setLocation(new UnaryExpression(UnaryOperator.LOGICAL_NOT, e)); :} 
	| BITWISE_NOT expression:e
		{: RESULT = setLocation(new UnaryExpression(UnaryOperator.BITWISE_NOT, e)); :} 
	| expression:e INCREMENT
		{: RESULT = setLocation(new UnaryExpression(UnaryOperator.INCREMENT_AND_RETURN_OLD, e)); :} 
	| INCREMENT expression:e
		{: RESULT = setLocation(new UnaryExpression(UnaryOperator.INCREMENT_AND_RETURN_NEW, e)); :} 
	| expression:e DECREMENT
		{: RESULT = setLocation(new UnaryExpression(UnaryOperator.DECREMENT_AND_RETURN_OLD, e)); :} 
	| DECREMENT expression:e
		{: RESULT = setLocation(new UnaryExpression(UnaryOperator.DECREMENT_AND_RETURN_NEW, e)); :} 
	| SUPPRESS_ERRORS expression:e
		{: RESULT = setLocation(new UnaryExpression(UnaryOperator.SUPPRESS_ERRORS, e)); :} 
	
	// binary operators
	| expression:e1 PLUS expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.ADD, e2)); :} 
	| expression:e1 MINUS expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.SUBTRACT, e2)); :} 
	| expression:e1 TIMES expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.MULTIPLY, e2)); :} 
	| expression:e1 DIVIDE expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.DIVIDE, e2)); :} 
	| expression:e1 MOD expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.REMAINDER, e2)); :} 
	| expression:e1 CONCAT expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.CONCATENATE, e2)); :} 
	| expression:e1 POWER expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.POWER, e2)); :} 
	| expression:e1 LOGICAL_SHORTCUT_AND expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.LOGICAL_SHORTCUT_AND, e2)); :} 
	| expression:e1 LOGICAL_SHORTCUT_OR expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.LOGICAL_SHORTCUT_OR, e2)); :} 
	| expression:e1 LOW_PRECEDENCE_LOGICAL_SHORTCUT_AND expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.LOGICAL_SHORTCUT_AND, e2)); :} 
	| expression:e1 LOW_PRECEDENCE_LOGICAL_SHORTCUT_OR expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.LOGICAL_SHORTCUT_OR, e2)); :} 
	| expression:e1 LOW_PRECEDENCE_LOGICAL_XOR expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.LOGICAL_XOR, e2)); :} 
	| expression:e1 BITWISE_AND expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.BITWISE_AND, e2)); :} 
	| expression:e1 BITWISE_OR expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.BITWISE_OR, e2)); :} 
	| expression:e1 BITWISE_XOR expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.BITWISE_XOR, e2)); :} 
	| expression:e1 SHIFT_LEFT expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.SHIFT_LEFT, e2)); :} 
	| expression:e1 SHIFT_RIGHT expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.SHIFT_RIGHT, e2)); :} 
	| expression:e1 EQUAL expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.EQUALS, e2)); :} 
	| expression:e1 NOT_EQUAL expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.NOT_EQUALS, e2)); :} 
	| expression:e1 IDENTICAL expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.IDENTICAL, e2)); :} 
	| expression:e1 NOT_IDENTICAL expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.NOT_IDENTICAL, e2)); :} 
	| expression:e1 GREATER expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.GREATER_THAN, e2)); :} 
	| expression:e1 LESS expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.LESS_THAN, e2)); :}
	| expression:e1 GREATER_EQUAL expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.GREATER_EQUAL, e2)); :} 
	| expression:e1 LESS_EQUAL expression:e2
		{: RESULT = setLocation(new BinaryExpression(e1, BinaryOperator.LESS_EQUAL, e2)); :}
	| expression:e1 ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.RIGHT, e2)); :}
	| expression:e1 ASSIGN BITWISE_AND expression:e2
		{: RESULT = setLocation(new ReferenceAssignmentExpression(e1, e2)); :}
	| expression:e1 PLUS_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.ADD, e2)); :} 
	| expression:e1 MINUS_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.SUBTRACT, e2)); :} 
	| expression:e1 TIMES_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.MULTIPLY, e2)); :} 
	| expression:e1 DIVIDE_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.DIVIDE, e2)); :} 
	| expression:e1 MOD_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.REMAINDER, e2)); :} 
	| expression:e1 CONCAT_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.CONCATENATE, e2)); :}
	| expression:e1 BITWISE_AND_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.BITWISE_AND, e2)); :} 
	| expression:e1 BITWISE_OR_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.BITWISE_OR, e2)); :} 
	| expression:e1 BITWISE_XOR_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.BITWISE_XOR, e2)); :} 
	| expression:e1 SHIFT_LEFT_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.SHIFT_LEFT, e2)); :} 
	| expression:e1 SHIFT_RIGHT_ASSIGN expression:e2
		{: RESULT = setLocation(new BinaryAssignmentExpression(e1, BinaryOperator.SHIFT_RIGHT, e2)); :}
	
	// ternary operator
	| expression:e1 QUESTION_MARK expression:e2 COLON expression:e3
		{: RESULT = setLocation(new ConditionalExpression(e1, e2, e3)); :}

	// arrays
	| ARRAY OPENING_PARENTHESIS array_literal_elements:els CLOSING_PARENTHESIS
		{: RESULT = setLocation(new ArrayConstructionExpression(els)); :}
	| OPENING_SQUARE_BRACKET array_literal_elements:els CLOSING_SQUARE_BRACKET
		{: RESULT = setLocation(new ArrayConstructionExpression(els)); :}
	| expression:a OPENING_SQUARE_BRACKET CLOSING_SQUARE_BRACKET
		{: RESULT = setLocation(new ArrayAppendExpression(a)); :}
	| expression:a OPENING_SQUARE_BRACKET expression:k CLOSING_SQUARE_BRACKET
		{: RESULT = setLocation(new ArrayElementExpression(a, k)); :}

	// functions
	| potentiallyDynamicName:name OPENING_PARENTHESIS expressions:exs CLOSING_PARENTHESIS
		{: RESULT = setLocation(ParserHelper.buildFunctionCallLikeExpression(namexleft, name, exs)); :}
		
	// special built-in expressions
	| INCLUDE expression:e
		{: RESULT = setLocation(new FunctionCall("include", e)); :}
	| INCLUDE_ONCE expression:e
		{: RESULT = setLocation(new FunctionCall("include_once", e)); :}
	| REQUIRE expression:e
		{: RESULT = setLocation(new FunctionCall("require", e)); :}
	| REQUIRE_ONCE expression:e
		{: RESULT = setLocation(new FunctionCall("require_once", e)); :}
	| PRINT expression:e
		{: RESULT = setLocation(new FunctionCall("print", e)); :}
	
	// OOP
	| NEW potentiallyDynamicName:name OPENING_PARENTHESIS expressions:exs CLOSING_PARENTHESIS
		{: RESULT = setLocation(new NewExpression(name, exs.toArray(new Expression[exs.size()]))); :}
	| expression:e INSTANCEOF IDENTIFIER:id
		{: RESULT = setLocation(new InstanceofExpression(e, id)); :}
	| potentiallyDynamicName:classId SCOPE_RESOLUTION potentiallyDynamicName:fieldId
		{: RESULT = setLocation(new StaticFieldAccess(classId, fieldId)); :}
	| SELF SCOPE_RESOLUTION potentiallyDynamicName:fieldId
		{: RESULT = setLocation(new StaticFieldAccess(null, fieldId)); :}
	| expression:obj RIGHT_ARROW potentiallyDynamicName:name
		{: RESULT = setLocation(new InstanceFieldAccess(obj, name)); :}
	| potentiallyDynamicName:classId SCOPE_RESOLUTION potentiallyDynamicName:methodId OPENING_PARENTHESIS expressions:exs CLOSING_PARENTHESIS
		{: RESULT = setLocation(new StaticMethodCall(classId, methodId, exs.toArray(new Expression[exs.size()]))); :}
	| SELF SCOPE_RESOLUTION potentiallyDynamicName:methodId OPENING_PARENTHESIS expressions:exs CLOSING_PARENTHESIS
		{: RESULT = setLocation(new StaticMethodCall(null, methodId, exs.toArray(new Expression[exs.size()]))); :}
	| PARENT SCOPE_RESOLUTION potentiallyDynamicName:methodId OPENING_PARENTHESIS expressions:exs CLOSING_PARENTHESIS
		{: if (1 == 1) throw new NotImplementedException(""); :}
	| expression:obj RIGHT_ARROW potentiallyDynamicName:name OPENING_PARENTHESIS expressions:exs CLOSING_PARENTHESIS
		{: RESULT = setLocation(new InstanceMethodCall(obj, name, exs.toArray(new Expression[exs.size()]))); :}


	// syntax error recovery
	| error
		{: RESULT = setLocation(new LiteralExpression(null)); :}
;

array_literal_elements ::=
		{: RESULT = new ArrayList<Pair<Expression, Expression>>(); :}
	| one_or_more_array_literal_elements:els
		{: RESULT = els; :}
	| one_or_more_array_literal_elements:els COMMA
		{: RESULT = els; :}
;

one_or_more_array_literal_elements ::=
	array_literal_element:el
		{: RESULT = new ArrayList<Pair<Expression, Expression>>(); RESULT.add(el); :}
	| one_or_more_array_literal_elements:els COMMA array_literal_element:el
		{: els.add(el); RESULT = els; :}
;

array_literal_element ::=
	expression:v
		{: RESULT = Pair.of(null, v); :}
	| expression:k DOUBLE_RIGHT_ARROW expression:v
		{: RESULT = Pair.of(k, v); :}
;

potentiallyDynamicName ::=
	IDENTIFIER:id
		{: RESULT = setLocation(new LiteralExpression(id)); :}
	| LOCAL_VARIABLE_SINGLE_INDIRECTION:id
		{: RESULT = setLocation(new LocalVariableExpression(id, 1)); :}
	| LOCAL_VARIABLE_MULTI_INDIRECTION:var
		{: RESULT = var; :}
;

// ------------------------------------------------------------------------------------
// --- function definition productions
// ------------------------------------------------------------------------------------

parameter_declarations ::=
		{: RESULT = new ArrayList<ParameterDeclaration>(); :}
	| one_or_more_parameter_declarations:decls
		{: RESULT = decls; :}
;

one_or_more_parameter_declarations ::=
	parameter_declaration:decl
		{: RESULT = new ArrayList<ParameterDeclaration>(); RESULT.add(decl); :}
	| one_or_more_parameter_declarations:decls COMMA parameter_declaration:decl
		{: decls.add(decl); RESULT = decls; :}
;

parameter_declaration ::=
	LOCAL_VARIABLE_SINGLE_INDIRECTION:v
		{: RESULT = new ParameterDeclaration(v, false); :}
	| LOCAL_VARIABLE_SINGLE_INDIRECTION:v ASSIGN expression:e
		{: RESULT = new ParameterDeclaration(v, false, e.evaluate(null)); // TODO catch errors due to missing environment :}
	| BITWISE_AND LOCAL_VARIABLE_SINGLE_INDIRECTION:v
		{: RESULT = new ParameterDeclaration(v, true); :}
	| BITWISE_AND LOCAL_VARIABLE_SINGLE_INDIRECTION:v ASSIGN expression:e
		{: RESULT = new ParameterDeclaration(v, true, e.evaluate(null)); // TODO catch errors due to missing environment :}
;

// ------------------------------------------------------------------------------------
// --- OOP type definition productions
// ------------------------------------------------------------------------------------

oop_type_definition ::= 
	CLASS IDENTIFIER:id oop_class_extends_clause oop_class_implements_clause OPENING_CURLY_BRACE oop_member_definitions:members CLOSING_CURLY_BRACE
		{: :}
	| INTERFACE IDENTIFIER:id oop_interface_extends_clause OPENING_CURLY_BRACE oop_member_definitions:members CLOSING_CURLY_BRACE
		{: :}
;

oop_class_extends_clause ::=
		{: :}
	| EXTENDS IDENTIFIER:id
		{: :}
;		

oop_class_implements_clause ::=
		{: :}
	| IMPLEMENTS interface_references:itfs
		{: :}
;		

oop_interface_extends_clause ::=
		{: :}
	| EXTENDS interface_references:itfs
		{: :}
;

interface_references ::=
		{: RESULT = new ArrayList<String>(); :}
	| one_or_more_interface_references:itfs
		{: RESULT = itfs; :}
;

one_or_more_interface_references ::=
	interface_reference:itf
		{: RESULT = new ArrayList<String>(); RESULT.add(itf); :}
	| one_or_more_interface_references:itfs COMMA interface_reference:itf
		{: itfs.add(itf); RESULT = itfs; :}
;

interface_reference ::=
	IDENTIFIER:id
		{: RESULT = id; :}
;

// ------------------------------------------------------------------------------------
// --- OOP member definition productions
// ------------------------------------------------------------------------------------

oop_member_definitions ::=
		{: RESULT = new ArrayList<OopMemberDefinition>(); :}
	| oop_member_definitions:mbrs oop_member_definition:mbr
		{: mbrs.add(mbr); RESULT = mbrs; :}
	;

oop_member_definition ::=
	CONST IDENTIFIER:id ASSIGN expression:e SEMICOLON
		{: :}
	| VAR LOCAL_VARIABLE_SINGLE_INDIRECTION:v SEMICOLON
		{: :}
	| VAR LOCAL_VARIABLE_SINGLE_INDIRECTION:v ASSIGN expression:e SEMICOLON
		{: :}
	| oop_entity_modifiers:mods LOCAL_VARIABLE_SINGLE_INDIRECTION:v SEMICOLON
		{: :}
	| oop_entity_modifiers:mods LOCAL_VARIABLE_SINGLE_INDIRECTION:v ASSIGN expression:e SEMICOLON
		{: :}
	| oop_entity_modifiers:mods FUNCTION IDENTIFIER:id OPENING_PARENTHESIS parameter_declarations:decls CLOSING_PARENTHESIS block:body
		{: :}
;

// ------------------------------------------------------------------------------------
// --- OOP helper productions
// ------------------------------------------------------------------------------------

oop_entity_modifiers ::=
		{: RESULT = new ArrayList<OopEntityModifier>(); :}
	| oop_entity_modifiers:mods oop_entity_modifier:mod
		{: mods.add(mod); RESULT = mods; :}
;

oop_entity_modifier ::=
	PUBLIC
		{: :}
	| PROTECTED
		{: :}
	| PRIVATE
		{: :}
	| STATIC
		{: :}
	| ABSTRACT
		{: :}
	| FINAL
		{: :}
;
