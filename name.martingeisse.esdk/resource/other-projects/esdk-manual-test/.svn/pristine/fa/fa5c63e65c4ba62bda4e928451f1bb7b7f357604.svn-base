
;
; Register usage:
;  0..? function arguments
;  C..F temporary (caller-save, unless otherwise noted)
;

start:
	CALL drawTitleScreen
loop:
	JUMP loop


;
; fillRangeRow(block, start, count, row): Fills (count) blocks in the specified row,
; starting at column (start), with block (block).
;
; Preserved arguments: block, row
; Preserves all temp registers.
;
fillRangeInRow:
	OUTPUT s3, 40
	; fall through

;
; fillRangeInCurrentRow(block, start, count): Fills (count) blocks in the current row,
; starting at column (start), with block (block).
;
; Preserved arguments: block
; Argument start is increased by count.
; Argument count is replaced by FF.
; Preserves all temp registers.
;
fillRangeInCurrentRow:
	SUB s2, 01
	RETURN C
	OUTPUT s0, s1
	ADD s1, 01
	JUMP fillRangeInCurrentRow

;
; fillRangeInColumn(block, column, (ignored), start, count): Fills (count) blocks in the specified column,
; starting at row (start), with block (block).
;
; Preserved arguments: block, column
; Argument start is increased by count.
; Argument count is replaced by FF.
; Preserves all temp registers.
;
fillRangeInColumn:
	SUB s4, 01
	RETURN C
	OUTPUT s3, 40
	OUTPUT s0, s1
	ADD s3, 01
	JUMP fillRangeInColumn

;
; fillRectangle(block, startColumn, columnCount, startRow, rowCount)
;
; Preserved arguments: block
; Preserves temp registers except sF.
;
fillRectangle:
	LOAD sF, s2					; save column count in sF
fillRectangle_loop:
	SUB s4, 01					; decrement row count
	RETURN C					; return if last row drawn
	CALL fillRangeInRow			; draw single row
	SUB s1, sF					; restore start column
	LOAD s2, sF					; restore column count
	ADD s3, 01					; increment start row
	JUMP fillRectangle_loop

;
; outlineRectangle(block, startColumn, columnCountDec, startRow, rowCountDec)
;
; Preserved arguments: block
; Preserves temp registers except sF. ?????
;
outlineRectangle:
	LOAD sF, s2					; save decremented column count in sF
	CALL fillRangeInRow			; draw top row
	SUB s1, sF					; restore start column
	LOAD s2, sF					; restore column count
	ADD s3, s4					; move to the bottom row
	CALL fillRangeInRow			; draw bottom row
	SUB s1, sF					; restore start column
	LOAD s2, sF					; restore column count
	SUB s3, s4					; move back the top row
	LOAD sF, s4					; save decremented row count in sF
	CALL fillRangeInColumn		; draw the left column
	SUB s3, sF					; restore start row
	LOAD s4, sF					; restore row count
	ADD s4, 01					; fill the lower right corner, too
	ADD s1, s2					; move to the right column
	CALL fillRangeInColumn		; draw the right column
	RETURN

;
; drawTitleScreen(): draws the title screen
;
; Preserved arguments:
; Preserves no temp registers.
;
drawTitleScreen:

	; draw outline
	LOAD s0, 08
	LOAD s1, 00
	LOAD s2, 27
	LOAD s3, 00
	LOAD s4, 1D
	CALL outlineRectangle

	; fill interior with blank blocks
	LOAD s0, 00
	LOAD s1, 01
	LOAD s2, 26
	LOAD s3, 01
	LOAD s4, 1C
	CALL fillRectangle
	
	RETURN
